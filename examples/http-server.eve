start path resolution
```
  match
     r = [#http-request]
     split[text:r.request.url token index, by:"/"]
     fr = [#root]
  bind
     [#path index:1 file:fr]
     [#path index token]
     [#content-type suffix:"jpeg" type:"application/jpeg"]
     [#content-type suffix:"css" type:"text/css"]
     [#content-type suffix:"js" type:"application/javascript"]
     [#content-type suffix:"html" type:"text/html"]
```

resolve a path
```
match
    [#path index:pindex file:pfile]
    f = pfile.child
    child = [#path index:(pindex + 1) token:f.name]
bind
    child.file := f
```

path search complete
```
match
     [#path index token]
     final = [#path index: count[given: index] file]
     split[text:final.token by:"." token:extoken index:exindex]
     exindex = 2
bind
     [#request-object extension:extoken token: final.token file]
```

complete! send content response
```
  match
     r = [#http-request]
     [#request-object extension file]
     ct = [#content-type suffix:extension]
  bind
     r.finished := true
     r.response := [
       content: file.contents
       status:"200"
       reason:"OK"
       header:[Content-Type:ct.type]]
```


initiate eve program
```
  match
     r = [#http-request request:[headers: [not(Sec-WebSocket-Protocol)]]]
     [#request-object extension:"eve" file]
     [#root child:[name:"index.html" contents]]
  bind
     r.finished := true
     r.response := [content: contents
       status:"200"
       reason:"OK"
       header:[Content-Type:"text/html"]]
```


start eve program
```
  match
     r = [#http-request request:[headers: [Sec-WebSocket-Protocol]]]
     [#request-object extension:"eve" file]
  commit @process
     // build an environment for the new process
     child = [#process source: file.contents]
  bind
     r.finished := true
     // upgrade, protocol, json and process bind is kinda hardwired for now
     r.upgrade := child
```

no such file!
```
   match
      r = [#http-request]
      not(r.finished)
   bind
      r.response :=
       [status:"404"
        reason:"No such object"
        header: [Spoogle: "nothing"]
        content: ""]
```
