start path resolution
```
  match
     r = [#http-request]
     split[text:r.request.url token index, by:"/"]
     fr = [#root]
  bind
     [#path index:1 file:fr]
     [#path index token]
     [#content-type suffix:"jpeg" type:"application/jpeg"]
     [#content-type suffix:"css" type:"text/css"]
     [#content-type suffix:"js" type:"application/javascript"]
     [#content-type suffix:"html" type:"text/html"]
```

resolve a path
```
match
    [#path index:pindex file:pfile]
    f = pfile.child
    child = [#path index:(pindex + 1) token:f.name]
bind
    child.file := f
```

path search complete
```
match
     connection = [#http-request]
     [#path index token]
     final = [#path index: count[given: index] file]
     split[text:final.token by:"." token:extoken index:exindex]
     exindex = 2
commit
     [#request-object extension: extoken connection token: final.token file]
```


complete! send content response
```
  match
     r = [#request-object extension file connection]
     ct = [#content-type suffix:extension]
  commit
    connection.response := [
       content: file.contents
       status:"200"
       reason:"OK"
       header:[Content-Type:ct.type]]
    r := none
  bind r.completed := true
```


initiate eve program
```
  match
     r = [#request-object connection connection:[request:[headers: [not(Sec-WebSocket-Protocol)]]]
                          extension:"eve" file]
     [#root child:[name:"index.html" contents]]
  commit
     connection.response := [
       content: contents
       status:"200"
       reason:"OK"
       header:[Content-Type:"text/html"]]
     r := none
  bind r.completed := true
```


start eve program
```
  match
     r = [#request-object request:[headers: [Sec-WebSocket-Protocol]]]
     [#request-object extension:"eve" file]
  commit @process
     // build an environment for the new process
     child = [#process source: file.contents]
  commit
     connection.upgrade := child
     r := none
  bind
     r.completed := true
```


no such file!
```
   match
      r = [#request-object not(completed) connection]
   commit
      connection.response :=
       [status:"404"
        reason:"No such object"
        header: [Spoogle: "nothing"]
        content: ""]
      r := none
```
